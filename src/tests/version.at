dnl Copyright (C) 2003 Derek Price, Ximbiot,
dnl			& The Free Software Foundation, Inc.
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2, or (at your option)
dnl any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.

AT_CVS_BANNER([CVS Version Strings])

AT_CVS_SETUP([version])

AT_CHECK([cvs init])dnl

#
# The complex sed script below:
#
#	1. Normalizes the version number.
#       2. Removes the client/server string since there is no reason this test
#	   should not pass regardless of whether the executable was compiled
#	   with or without client and server support.
#	3. Normalizes the second year in the copyright notice, _except_ when
#	   the version of CVS being tested matches the version that this suite
#	   was built specifically for and the year is not the current one.
#	   This is so that this test will not fail when testing old versions of
#          CVS yet it will fail when testing the current CVS and so spot when
#	   the copyright notice has not been properly updated for the next
#	   release (during `make distcheck', for example).
#	   
# The s# *$## command is necessary to test old versions of CVS since some are
# known to have generated trailing spaces on some lines of their version string.
this_year=`date +%Y`
suite_version=`echo AT_PACKAGE_VERSION |sed 's/\./\\\\./g'`
AT_CHECK(
[cvs --version |
  sed -e 't clear' \
      -e ': clear' \
      -e "s# $suite_version ([a-z/]*)\$# VERSION#;s# $suite_version\$# VERSION#" \
      -e 't alt' \
      -e 's# 1[.][0-9][.0-9p]* ([a-z/]*)$# VERSION#;s# 1[.][0-9][.0-9p]*$# VERSION#' \
      -e 's# (c) 1989-[12][0-9][0-9][0-9] Brian # (c) 1989-YEAR Brian #' \
      -e 's# *$##' \
      -e 'b end' \
      -e ': alt' \
      -e 'n' \
      -e "s# (c) 1989-$this_year Brian # (c) 1989-YEAR Brian #" \
      -e 's# *$##' \
      -e 'b alt' \
      -e ': end'],
[0],
[
Concurrent Versions System (CVS) VERSION

Copyright (c) 1989-YEAR Brian Berliner, david d `zoo' zuhn,
                        Jeff Polk, and other authors

CVS may be copied only under the terms of the GNU General Public License,
a copy of which can be found with the CVS distribution kit.

Specify the --help option for further information about CVS
])



AT_CVS_REMOTE(
[AT_CHECK([cvs version |
  sed 's#^\(Client.*\)  *1[.][0-9][.0-9p]* (client[a-z/]*)$#\1 (client)#
       s#^\(Server.*\)  *1[.][0-9][.0-9p]* ([a-z/]*server)$#\1 (server)#'], [0],
[Client: Concurrent Versions System (CVS) (client)
Server: Concurrent Versions System (CVS) (server)
])])



AT_CVS_LOCAL(
[AT_CHECK([cvs version |
  sed 's#  *1[.][0-9][.0-9p]* ([a-z/]*)$##;s#  *1[.][0-9][.0-9p]*$##'], [0],
[Concurrent Versions System (CVS)
])])



AT_CLEANUP # version
